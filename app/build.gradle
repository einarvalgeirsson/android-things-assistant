apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.einarvalgeirsson'

android {
    compileSdkVersion 25
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 25
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        debug.java.srcDirs += ['src/main/java']
        release.java.srcDirs += ['src/main/java']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.7.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:support-annotations:27.0.1'

    compile 'io.grpc:grpc-okhttp:1.7.0'
    compile 'io.grpc:grpc-protobuf-lite:1.7.0'
    compile 'io.grpc:grpc-stub:1.7.0'
    compile 'io.grpc:grpc-auth:1.7.0'
    implementation('com.google.auth:google-auth-library-oauth2-http:0.6.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    implementation 'javax.annotation:javax.annotation-api:1.2'

    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testImplementation 'org.amshove.kluent:kluent:1.28'
    testImplementation 'org.json:json:20171018'

    compile project(':pocketsphinx-android-5prealpha-release')
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    mavenCentral()
}
